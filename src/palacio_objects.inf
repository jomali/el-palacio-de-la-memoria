

!!==============================================================================
!!
!!	EL PALACIO DE LA MEMORIA
!!	Definición de localidades y objetos
!!
!!==============================================================================
!!
!!	Archivo:		palacio_objects.inf
!!	Autor(es):		J. Francisco Martín <jfm.lisaso@gmail.com>
!!	Idioma:			ES (Español)
!!	Sistema:		Inform-INFSP 6
!!	Plataforma:		Máquina-Z/Glulx
!!	Versión:		0.0
!!	Fecha:			2018/03/04
!!
!!------------------------------------------------------------------------------
!!
!!	Este archivo forma parte del relato interactivo
!!	EL PALACIO DE LA MEMORIA.
!!
!!	EL PALACIO DE LA MEMORIA es software libre: usted puede
!!	redistribuirlo y/o modificarlo bajo los términos de la Licencia
!!	Pública General GNU publicada por la Fundación para el Software
!!	Libre, ya sea la versión 3 de la Licencia, o (a su elección)
!!	cualquier versión posterior.
!!
!!	EL PALACIO DE LA MEMORIA se distribuye con la esperanza de que sea
!!	útil, pero SIN GARANTÍA ALGUNA; ni siquiera la garantía implícita
!!	MERCANTIL o de APTITUD PARA UN PROPÓSITO DETERMINADO. Consulte los
!!	detalles de la Licencia Pública General GNU para más información.
!!
!!	Debería haber recibido una copia de la Licencia Pública General GNU
!!	junto a EL PALACIO DE LA MEMORIA. En caso contrario, consulte
!!	<http://www.gnu.org/licenses/>.
!!
!!	Copyright (c) 2018, J. Francisco Martín
!!
!!------------------------------------------------------------------------------
!!
!!	HISTORIAL DE VERSIONES
!!
!!	0.0: 2018/03/04	Inicio de desarrollo.
!!
!!------------------------------------------------------------------------------
!!
!!	TABLA DE CONTENIDOS
!!
!!	1)	Dungeon Master / Limbo
!!	2)	Baño
!!	3)	Cala de los cangrejos
!!	4)	Cocina
!!	5)	Dormitorio principal
!!	6)	Habitación multiusos
!!	7)	Pasillo de la segunda planta
!!	8)	Patio delantero
!!	9)	Recibidor
!!	10)	Salón
!!	11)	Taller
!!	12)	Terraza trasera
!!
!!------------------------------------------------------------------------------


!!==============================================================================
!!
!!	1)	Dungeon Master / Limbo
!!
!!------------------------------------------------------------------------------

Object	dungeon_master "(Dungeon Master)"
;

Room	limbo "En algún lugar"
 with	name_f	'babia',
		name_m	'limbo' 'lugar' 'sueno' 'vacio',
		gender	G_MASCULINO,
		description [;
			O__M(self, 1);
			if (self hasnt visited) O__M(self, 2);
			new_line;
			return true;
		],
		clarification 0,
 has	male;

NPC		-> silvia "Silvia"
 with	name_f	'personaje' 'protagonista' 'silvia',
		gender	G_FEMENINO,
		adjectives 'principal',
		description [; <<Inv>>; ],
 has	female proper;

Item	-> -> llavero_padre "llavero de su padre"
 with	name_f	'llave',
		name_fp	'llaves',
		name_m	'llavero',
		gender	G_MASCULINO,
		adjectives 'padre' 'su',
		article	"el",
		description [; return O__M(self, 1); ],
 has	male;


!!==============================================================================
!!
!!	2)	Baño
!!
!!------------------------------------------------------------------------------

Indoors	banno "Baño"
 with	name_f	'estancia' 'habitacion',
		name_m	'aseo' 'bano',
		gender	G_FEMENINO,
 has	female;


!!==============================================================================
!!
!!	3)	Cala de los cangrejos
!!
!!------------------------------------------------------------------------------

Outdoors	cala "Cala de los cangrejos"
 with	name_f	'abra' 'bahia' 'cala' 'caleta' 'costa' 'ensenada' 'orilla'
				'playa' 'rada',
		name_m	'ancon' 'grao' 'litoral',
		gender	G_FEMENINO,
		adjectives 'cangrejos',
 has	female;


!!==============================================================================
!!
!!	4)	Cocina
!!
!!------------------------------------------------------------------------------

Indoors	cocina "cocina"
 with	name_f	'cocina' 'estancia' 'habitacion',
		gender	G_FEMENINO,
		description [;
			return O__M(self, 1);
		],
		e_to	salon,
 has	female;

Atrezzo	-> cocina_encimera "encimera"
 with	name_f	'encimera',
		name_m	'mobiliario' 'mueble',
		name_mp	'muebles',
		gender	G_FEMENINO,
		description [; return O__M(self, 1); ],
 has	female supporter transparent;

Furniture	-> cocina_frigorifico "frigorífico"
 with	name_m	'electrodomestico' 'frigorifico' 'mobiliario' 'mueble',
		name_mp	'electrodomesticos' 'muebles',
		gender	G_MASCULINO,
		description [; return O__M(self, 1); ],
 has	concealed male;

Furniture	-> cocina_mesa "mesa"
 with	name_f	'mesa',
		name_m	'mobiliario' 'mueble',
		name_mp	'muebles',
		gender	G_FEMENINO,
		description [; return O__M(self, 1); ],
 has	female supporter transparent;

Item	-> -> bolso "bolso"
 with	name_f	'bandolera' 'bolsa',
		name_m	'bolso',
		gender	G_MASCULINO,
		adjectives 'silvia' 'su',
		article "su",
		description [; return O__M(self, 1); ],
 has	container male open;

Item	-> -> -> llavero_silvia "llavero de silvia"
 with	name_f	'llave',
		name_fp	'llaves',
		name_m	'llavero',
		gender	G_MASCULINO,
		adjectives 'silvia' 'su',
		article	"el",
		description [; return O__M(self, 1); ],
 has	male;


!!==============================================================================
!!
!!	5)	Dormitorio principal
!!
!!------------------------------------------------------------------------------

Indoors	dormitorio "Dormitorio principal"
 with	name_f	'estancia' 'habitacion',
		name_m	'cuarto' 'dormitorio',
		gender	G_MASCULINO,
		adjectives 'principal',
 has	male;

Furniture	-> dormitorio_cama "cama"
 with	name_f	'cama',
		name_m	'mobiliario' 'mueble',
		gender	G_FEMENINO,
		description [; return O__M(self, 1); ],
 has	enterable female supporter transparent;


!!==============================================================================
!!
!!	6)	Habitación multiusos
!!
!!------------------------------------------------------------------------------

Indoors	habitacion "Habitación multiusos"
 with	name_f	'estancia' 'habitacion',
		name_m	'cuarto',
		gender	G_FEMENINO,
 has	female;


!!==============================================================================
!!
!!	7)	Pasillo de la segunda planta
!!
!!------------------------------------------------------------------------------

Indoors	pasillo "Pasillo de la segunda planta"
 with	name_f	'estancia' 'habitacion',
		name_m	'corredor' 'pasillo',
		gender	G_MASCULINO,
 has	male;

Atrezzo	-> fotografias "fotografías"
 with	name_f	'foto' 'fotografias',
		gender	G_FEMENINO + G_PLURAL,
		description [; return O__M(self, 1); ],
 has	female pluralname;


!!==============================================================================
!!
!!	8)	Patio delantero
!!
!!------------------------------------------------------------------------------

Indoors	patio "Patio delantero"
 with	name_f	'entrada' 'terraza',
		name_m	'corral' 'patio',
		gender	G_MASCULINO,
		adjectives 'delantera' 'delantero',
		description [;
			return O__M(self, 1);
		],
		n_to	puerta_delantera,
		in_to	[; return self.n_to(); ],
 has	male;

Atrezzo	-> patio_coche "coche"
 with	name_m	'coche' 'vehiculo',
		gender	G_MASCULINO,
		description [; return O__M(self, 1); ],
 has	enterable male ~open openable;

Furniture	puerta_delantera "puerta de entrada"
 with	found_in patio recibidor,
		name_f	'entrada' 'puerta',
		gender	G_FEMENINO,
		adjectives 'delantera' 'principal',
		desription [;
			if (location == patio) return O__M(self, 1);
			return O__M(self, 2);
		],
		door_dir [;
			if (location == patio) return n_to;
			return s_to;
		],
		door_to [;
			if (location == patio) return recibidor;
			return patio;
		],
 has	concealed door female ~open openable;

Atrezzo	cristalera "cristalera de colores"
 with	found_in patio recibidor,
		name_f	'cristalera',
		name_m	'panel',
		adjectives
			'coloreada' 'colores' 'policromada' 'policromado' 'tintada'
			'tintado' 'traslúcida' 'traslúcido',
		description [;
			if (location == patio) return O__M(self, 1);
			return O__M(self, 2);
		],
 has	female;


!!==============================================================================
!!
!!	9)	Recibidor
!!
!!------------------------------------------------------------------------------

!!	-	'general' se activa tras abandonar la localidad por primera vez
Indoors	recibidor "Recibidor"
 with	name_f	'antesala' 'estancia' 'habitacion',
		name_m	'hall' 'recibidor' 'vestibulo',
		gender	G_MASCULINO,
		description [;
			if (self hasnt visited) {
				O__M(dungeon_master, 4);
				new_line;
				return O__M(self, 4);
			}
			if (puerta_delantera hasnt open) O__M(self, 1);
			else O__M(self, 2);
			if (self has general) {
				print " ";
				return O__M(self, 3);
			} else {
				new_line;
				return true;
			}
		],
		after [;
			Go:
				if (self hasnt general)
					give self general;
		],
		n_to	salon,
		s_to	puerta_delantera,
		out_to	[; return self.s_to(); ],
 has	male;

Item	-> recibidor_perchero "perchero alto"
 with	name_f	'percha',
		name_m	'perchero',
		gender	G_MASCULINO,
		adjectives 'alto',
		describe [
			obj i;
			if (child(self) == false) return true;
			objectloop(obj in self && obj hasnt concealed) i++;
			if (i == 0) return true;
			new_line;
			if (i == 1 && child(self) == abrigo) return O__M(self, 4);
			if (i == 1) return O__M(self, 5);
			return O__M(self, 6);
		],
		description [
			obj i;
			O__M(self, 1);
			if (child(self) == false) ".";
			objectloop(obj in self && obj hasnt concealed) i++;
			if (i == 0) ".";
			if (i == 1) return O__M(self, 2);
			return O__M(self, 3);
		],
 has	male supporter transparent;

Item	-> -> abrigo "abrigo"
 with	name_m	'abrigo',
		gender	G_MASCULINO,
 has	clothing male;

Furniture	-> recibidor_comoda "cómoda"
 with	name_f	'comoda',
		name_m	'mobiliario' 'mueble',
		gender	G_FEMENINO,
		adjectives 'madera',
		description [; return O__M(self, 1); ],
 has	concealed female;


!!==============================================================================
!!
!!	10)	Salón
!!
!!------------------------------------------------------------------------------

Indoors	salon "Salón"
 with	name_f	'estancia' 'habitacion' 'sala',
		name_m	'comedor' 'salon',
		gender	G_MASCULINO,
		description [;
			return O__M(self, 1);
		],
		s_to	recibidor,
		w_to	cocina,
 has	male;

Atrezzo	-> salon_ventanales "ventanales"
 with	name_f	'ventana',
		name_fp	'ventanas',
		name_m	'ventanal',
		name_mp	'ventanales',
		gender	G_MASCULINO + G_PLURAL,
		description [; return O__M(self, 1); ],
 has	male pluralname;

Atrezzo	-> salon_estanterias "estanterías"
 with	name_f	'estanteria',
		name_fp	'estanterias',
		name_m	'estante',
		name_mp	'estantes',
		gender	G_FEMENINO + G_PLURAL,
		description [; return O__M(self, 1); ],
 has	female pluralname supporter transparent;

Atrezzo	-> -> salon_libros "libros"
 with	name_m	'libro',
		name_mp	'libros',
		gender	G_MASCULINO + G_PLURAL,
		description [; return O__M(self, 1); ],
 has	male pluralname;

Item	-> -> frasco "adorno"
 with	name_m	'adorno' 'frasco',
		gender	G_MASCULINO,
		description [; return O__M(self, 1); ],
 has	concealed male;

Atrezzo	-> salon_mueblebar "mueblebar"
 with	name_f	'bar',
		name_m	'mobiliario' 'mueble' 'mueblebar',
		gender	G_MASCULINO,
		description [; return O__M(self, 1); ],
 has	male ~open openable;

Furniture	-> salon_sofa "sofá"
 with	name_m	'sofa',
		gender	G_MASCULINO,
		description [; return O__M(self, 1); ],
 has	concealed enterable male supporter transparent;

Furniture	-> salon_televisor "televisor"
 with	name_f	'television',
		name_m	'televisor',
		gender	G_MASCULINO,
		description [; return O__M(self, 1); ],
 has	concealed male;


!!==============================================================================
!!
!!	11)	Taller
!!
!!------------------------------------------------------------------------------

Indoors	taller "Taller"
 with	name_m	'cuarto' 'taller',
		gender	G_MASCULINO,
 has	male;


!!==============================================================================
!!
!!	12)	Terraza trasera
!!
!!------------------------------------------------------------------------------

Outdoors	terraza "Terraza trasera"
 with	name_f	'terraza',
		name_m	'mirador' 'patio',
		gender	G_FEMENINO,
		adjectives 'trasera' 'trasero',
 has	female;
